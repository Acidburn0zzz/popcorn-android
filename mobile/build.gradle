apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "pct.droid"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName "0.1"
    }

    signingConfigs {
        mainConfig {
            keyAlias System.getenv("S_ALIAS").toString() ?: ""
            keyPassword System.getenv("S_KEYPASS").toString() ?: ""
            storeFile file(System.getenv("S_KEYSTORE").toString()) ?: file("")
            storePassword System.getenv("S_KEYPASS").toString() ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.mainConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "#"+(System.getenv("BUILD_NUMBER") ?: "dev")
        }
        jnidebug.initWith(buildTypes.debug)
        jnidebug {
            jniDebuggable true
        }
    }

    lintOptions {
        abortOnError false
    }

    splits {
        abi {
            enable true
            universalApk false
            reset()
            include 'armeabi', 'armeabi-v7a', 'x86', 'arm64-v8a'
        }
    }

    project.ext.versionCodes = ["armeabi": 1, "armeabi-v7a": 2, "arm64-v8a": 3, "x86": 4]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI);
            output.versionCodeOverride = project.ext.versionCodes.get(abi, 0) * 1000000 + defaultConfig.versionCode

            def alignedOutputFile = output.outputFile
            def unalignedOutputFile = output.packageApplication.outputFile

            // Customise APK filenames (to include build version)
            if (variant.buildType.zipAlignEnabled) {
                // normal APK
                output.outputFile = new File(alignedOutputFile.parent, alignedOutputFile.name.replace("debug", "development").replace(".apk", "-v" + defaultConfig.versionName + ".apk"))
            }
            // 'unaligned' APK
            output.packageApplication.outputFile = new File(unalignedOutputFile.parent, unalignedOutputFile.name.replace("debug", "development").replace(".apk", "-v" + defaultConfig.versionName + ".apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:palette-v7:21.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.sothree.slidinguppanel:library:2.0.2'
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:mediarouter-v7:21.0.0'
    compile project(':base')
    compile project(':vlc')
    compile project(':node')
    compile project(':xmlrpc')
}